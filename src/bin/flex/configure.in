dnl Process this file with autoconf to produce a configure script.

dnl This file is part of flex.

dnl Redistribution and use in source and binary forms, with or without
dnl modification, are permitted provided that the following conditions
dnl are met:

dnl 1. Redistributions of source code must retain the above copyright
dnl notice, this list of conditions and the following disclaimer.
dnl 2. Redistributions in binary form must reproduce the above copyright
dnl notice, this list of conditions and the following disclaimer in the
dnl documentation and/or other materials provided with the distribution.

dnl Neither the name of the University nor the names of its contributors
dnl may be used to endorse or promote products derived from this software
dnl without specific prior written permission.

dnl THIS SOFTWARE IS PROVIDED ``AS IS'' AND WITHOUT ANY EXPRESS OR
dnl IMPLIED WARRANTIES, INCLUDING, WITHOUT LIMITATION, THE IMPLIED
dnl WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
dnl PURPOSE.

dnl autoconf requirements and initialization

AC_PREREQ(2.54)
AC_INIT(flex,2.5.33,flex-help@lists.sourceforge.net)
AC_CONFIG_SRCDIR(scan.l)
AM_INIT_AUTOMAKE
AC_CONFIG_HEADER(config.h:conf.in)

dnl checks for programs

AM_GNU_GETTEXT([external])
AM_GNU_GETTEXT_VERSION(0.12)

AC_PROG_YACC
AM_PROG_LEX
AC_PROG_CC
AC_PROG_CXX
AC_PROG_LN_S
AC_PROG_RANLIB

AC_PATH_PROG(BISON, bison,bison)
AC_PATH_PROG(HELP2MAN, help2man, help2man)


# Check for a GNU m4 that supports --prefix-builtins
AC_PATH_PROGS(M4, gm4 gnum4 m4, m4)
if test x"$M4" != x; then
  AC_MSG_CHECKING([for GNU m4])
  case `$M4 --help < /dev/null 2>&1` in
    *prefix-builtins*) AC_MSG_RESULT(yes) ;;
    *) AC_MSG_RESULT(no) ;
       AC_MSG_ERROR([GNU M4 1.4 is required]) ;;
  esac
else
   AC_MSG_ERROR([GNU M4 1.4 is required]) ;
fi
AC_DEFINE_UNQUOTED([M4], ["$M4"], [Define to the GNU M4 executable name.])

AC_PATH_PROG(INDENT, indent, indent)
dnl if INDENT is set to 'indent' then we didn't find indent
if test "$INDENT" != indent ; then
   AC_MSG_CHECKING(if $INDENT is GNU indent)
   if $INDENT --version 2>/dev/null | head -n 1|grep "GNU indent" > /dev/null ; then
      AC_MSG_RESULT(yes)
   else
      AC_MSG_RESULT(no)
      AC_MSG_WARN($INDENT does not appear to be GNU indent.)
   fi
else
   AC_MSG_WARN(no indent program found: make indent target will not function)
fi

dnl checks for libraries
AC_CHECK_LIB(m, log)

dnl checks for header files

AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h stdbool.h  netinet/in.h limits.h)
AC_CHECK_HEADERS( sys/wait.h sys/params.h)
AC_CHECK_HEADERS(cunistd)
AC_CHECK_HEADERS(locale.h libintl.h)
AC_CHECK_HEADERS(regex.h)

dnl checks for types

AC_TYPE_SIZE_T

dnl checks for functions

AC_CHECK_DECLS(__func__)

AC_CONFIG_FILES(
flex.spec
Makefile
doc/Makefile
examples/Makefile
examples/fastwc/Makefile
examples/manual/Makefile
m4/Makefile
po/Makefile.in
tools/Makefile
tests/Makefile
tests/TEMPLATE/Makefile
tests/test-array-nr/Makefile
tests/test-array-r/Makefile
tests/test-basic-nr/Makefile
tests/test-basic-r/Makefile
tests/test-bison-yylloc/Makefile
tests/test-bison-yylval/Makefile
tests/test-c-cpp-nr/Makefile
tests/test-c-cpp-r/Makefile
tests/test-header-nr/Makefile
tests/test-header-r/Makefile
tests/test-include-by-buffer/Makefile
tests/test-include-by-push/Makefile
tests/test-include-by-reentrant/Makefile
tests/test-multiple-scanners-nr/Makefile
tests/test-multiple-scanners-r/Makefile
tests/test-noansi-nr/Makefile
tests/test-noansi-r/Makefile
tests/test-prefix-nr/Makefile
tests/test-prefix-r/Makefile
tests/test-pthread/Makefile
tests/test-string-nr/Makefile
tests/test-string-r/Makefile
tests/test-yyextra/Makefile
tests/test-lineno-nr/Makefile
tests/test-lineno-r/Makefile
tests/test-linedir-r/Makefile
tests/test-debug-r/Makefile
tests/test-debug-nr/Makefile
tests/test-mem-nr/Makefile
tests/test-mem-r/Makefile
tests/test-posix/Makefile
tests/test-posixly-correct/Makefile
tests/test-table-opts/Makefile
tests/test-c++-basic/Makefile
tests/test-bison-nr/Makefile
tests/test-reject/Makefile
tests/test-c++-multiple-scanners/Makefile
tests/test-top/Makefile
tests/test-rescan-nr/Makefile
tests/test-rescan-r/Makefile
dnl --new-test-here-- This line is processed by tests/create-test.
)

AC_OUTPUT
